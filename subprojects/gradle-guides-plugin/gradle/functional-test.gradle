sourceSets {
    functionalTest {
        groovy.srcDir file('src/functionalTest/groovy')
        resources.srcDir file('src/functionalTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

task functionalTest(type: Test) {
    description = 'Runs the functional tests.'
    group = 'verification'
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    shouldRunAfter test
    maxParallelForks = 2
    systemProperty("gradle.version", gradle.gradleVersion)

    reports {
        html.outputLocation = project.file("${html.outputLocation.get().asFile}/functional")
        junitXml.outputLocation = project.file("${junitXml.outputLocation.get().asFile}/functional")
    }
}

check.dependsOn functionalTest

gradlePlugin {
    testSourceSets sourceSets.functionalTest
}
